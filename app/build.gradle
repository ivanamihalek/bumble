apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.dogmaticcentral.bumble"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "size"
    productFlavors {
        small {
            // Assigns this product flavor to the "version" flavor dimension.
            // If you are using only one dimension, this property is optional,
            // and the plugin automatically assigns all the module's flavors to
            // that dimension.
            dimension "size"
            applicationIdSuffix ".small"
            versionNameSuffix "-small"
        }

        full {
            dimension "size"
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }

        local {
            dimension "size"
            applicationIdSuffix ".local"
            versionNameSuffix "-local"
        }
    }

    sourceSets{
        local {
            res.srcDirs = [
                    'src/local/res',
                    '../feat1/src/main/res',
                    '../feat2/src/main/res'
             ]
        }
    }

    dynamicFeatures = [":feat1", ":feat2"]

    variantFilter { variant ->
         def names = variant.flavors*.name
         if(variant.buildType.name == 'release' && names.contains("local")) {
            setIgnore(true)
        }

    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    smallImplementation 'com.google.android.play:core:1.6.1'
}